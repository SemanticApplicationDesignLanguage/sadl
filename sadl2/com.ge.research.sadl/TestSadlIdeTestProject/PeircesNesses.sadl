uri "http://sadl.imp/PeircesNesses".

// Firstness is the classification that something receives because of its intrinsic
//   properties.
Firstness is a top-level class.

// Secondness is the classification that something receives because of its relationship to
//   something else; it is also known as a Role
Secondness is a top-level class.

// Thirdness is the mediating concept that brings two other concepts into relation
Thirdness is a top-level class.

Person is a type of Firstness.
Woman is a type of Person.	
Mother is a type of Secondness.
Child is a type of Secondness.	

BirthEvent is a type of Thirdness.	// puts Mother and Child into relationship
motherIn describes BirthEvent has values of type Woman.
childIn describes BirthEvent has values of type Person.
location describes BirthEvent has values of type string.
when describes BirthEvent has values of type date.

child describes Person has values of type Person.
mother describes Person has values of type Woman.

a Woman is a Mother only if child has at least 1 values.
a Person is a Child only if mother has at least 1 values.

Rule ReduceBirthEvent
	given
		x is a BirthEvent
	if
		m is motherIn of x
		c is childIn of x
	then
		m is mother of c 
		c is child of m .

Eve is a Woman.
Cain is a Person.
CainsBirth is a BirthEvent, has motherIn Eve, has childIn Cain, has location "outside Eden".

Test: Eve has child Cain.
Test: Cain has mother Eve.
Print: "This test will only pass if OWL entailments are enabled in the reasoner.".
Test: Eve is a Mother.
