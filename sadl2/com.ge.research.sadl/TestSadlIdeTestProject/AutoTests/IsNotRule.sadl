uri "http://sadl.imp/IsNotRule" .

Color is a top-level class, must be one of {Red, Yellow, Green}.

Action is a top-level class, must be one of {Go, Stop}.

TrafficLight is a top-level class.
color describes TrafficLight has values of type Color.

Vehicle is a top-level class.
action describes Vehicle has values of type Action.
approaching describes Vehicle with values of type TrafficLight.

Rule Rule_Go
	given
		x is a Vehicle
		y is a TrafficLight
	if
		x approaching y
		Green is color of y 	// alternative wording 1
		color of y is Green 	// alternative wording 2
	then
		action of x is Go . 
		
Rule Rule_Stop
	given
		x is a Vehicle
		y is a TrafficLight 
	if
		x approaching y, 
		y has color not Green 
		Green is not color of y // alternative wording 1
		color of y is not Green	// alternative wording 2
	then
		action of x is Stop . 
	
MyCar is a Vehicle approaching ThisLight.	 
ThisLight is a TrafficLight, has color Green. 

YourCar is a Vehicle, approaching ThatLight.
ThatLight is a TrafficLight has color Red.

Test: MyCar has action Go. 
Test: YourCar has action Stop.
Explain: Rule Rule_Go.
Explain: Rule Rule_Stop.
Ask: select x, y where x approaching y .

 