 uri "http://sadl.org/UsingListExpression.sadl" alias UsingListExpression.
 
 YooHoo is a class.
 YooHooList is a type of YooHoo List, described by selectedElement with values of type YooHoo.
 l1 is the YooHooList [yh1, yh2, yh3, yh4].
 l2 is the YooHoo List [yha, yhb, yhc].
 
 yh5 is a YooHoo.
 

// Expr: element before yh1 in l1.
// Expr: element after yh1 in l1.
// 
// 
// Expr: last element of l1.
// Expr: first element of l1.
// Expr: element 2 of l1.
// Expr: index of yh1 in l1.
// Expr: length of l1.
 
 Rule ListFirst:
   if l1 is a YooHooList and 
   		first element of l1 is le1 then l1 has selectedElement le1 and print("ListFirst: ", le1).
   		
 Rule ListLast:
 	if l1 is a YooHooList and last element of l1 is lel then print("Last element of l1: ", lel).   	
 	
 Rule ListAfter:
 	if l1 is a YooHooList and element after yh2 in l1 is lea then print("Element after yh2: ", lea). 
   
 Rule ListBefore:
 	if l1 is a YooHooList and element before yh3 in l1 is leb then print("Element before yh3: ", leb). 

 Rule ListElementAt:
 	if l1 is a YooHooList and element 3 of l1 is le3 then print("Element 3: ", le3). 

 Rule ListIndex:
 	if l1 is a YooHooList and index of yh2 in l1 is idx then print("Index of yh2: ", idx). 
 	
 Rule ListLength:
 	if l1 is a YooHooList and len is length of l1 then print("Length of l1: ", len). 
 	
 Rule ListInsert:
 	if l1 is a YooHooList and l1Added is insertElementInList(l1, yh5, 2) then print("l1Added: ", sadlListToString(l1Added)).
 	
 Rule ListDelete:
 	if l1 is a YooHooList and l1Deleted is deleteElementFromList(l1, 1) then print("l1Deleted: ", sadlListToString(l1Deleted)). 	
 

 Rule ListFirst2:
   if lst is a YooHooList and 
   		first element of lst is le1 then lst has selectedElement le1 and print("ListFirst2: ", le1).

 Ask: select x, y where x is a YooHooList and x has selectedElement y. 
  